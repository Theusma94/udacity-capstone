plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-android-extensions'
    id 'com.google.gms.google-services'
}

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
def keyAliasName = localProperties.getProperty('KEY_ALIAS_NAME')
def keyAliasPassword = localProperties.getProperty('KEY_ALIAS_PASSWORD')
def keyStorePassword = localProperties.getProperty('KEY_STORE_PASSWORD')

Properties adProperties = new Properties()
adProperties.load(project.rootProject.file('ad.properties').newDataInputStream())
def adAppId = adProperties.getProperty('AD_APP_ID')
def adInterId = adProperties.getProperty('AD_INTER_ID')
def adBannerId = adProperties.getProperty('AD_BANNER_ID')

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.theusmadev.coronareminder"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        android.defaultConfig.vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "AD_INTER_ID", "\""+adInterId+"\""
        resValue "string", "ad_banner_id", "\""+adBannerId+"\""
        resValue "string", "app_id", "\""+adAppId+"\""
    }

    signingConfigs {
        key {
            storeFile file('../upload-keys/keystore.jks')
            keyAlias keyAliasName
            keyPassword keyAliasPassword
            storePassword keyStorePassword
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.key
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$archLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$archLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$archLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$archLifecycleVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "com.google.firebase:firebase-auth:$firebaseAuthVersion"
    implementation 'com.google.firebase:firebase-ads:19.7.0'
    implementation "com.google.android.gms:play-services-auth:$playservicesVersion"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$junitTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"

    //Anko
    implementation "org.jetbrains.anko:anko:$ankoVersion"

    //Room dependencies
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    //Coroutines Dependencies
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    //Koin
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"

    //Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation("com.squareup.okhttp3:okhttp:$okhttpVersion")

    //Preference
    implementation "androidx.preference:preference-ktx:$preferenceVersion"

    //Multidex
    implementation "androidx.multidex:multidex:$multidexVersion"

    //Picasso
    implementation "com.squareup.picasso:picasso:$picassoVersion"
}